function series impute (const series y, const string method)
    /* Public function for imputing missing values of 'y' by some 'method'.
    return: series, imputed series. */

    string datatype = $datatype == 3 ? "panel" : "else"
    # Get mapped function parameters - also depends on datatype
    bundle Func = method_to_function_map(method)["@datatype"]

    if Func.dtype == "series"
        series ret = missing(y) ? feval(Func.method, y) : y
    else
        funcerr "Case not covered, yet."
    endif

    return ret
end function


function bundle method_to_function_map (const string method)
    /* Mapping method to function name being called.
    return: string, Function name being called. */

    bundle Map = null

    Map.min = _(\
                panel = _(method = "pmin", dtype = "series"),\
                else = _(method = "min", dtype = "series")\
                )
    Map.max = _(\
                panel = _(method = "pmax", dtype = "series"),\
                else = _(method = "max", dtype = "series")\
                )
    Map.mean = _(\
                panel = _(method = "pmean", dtype = "series"),\
                else = _(method = "mean", dtype = "series")\
                )
    Map.median = _(\
                panel = _(method = "pmedian", dtype = "series"),\
                else = _(method = "median", dtype = "series")\
                )
    Map.cum = _(\
                panel = _(method = "cum", dtype = "series"),\
                else = _(method = "cum", dtype = "series")\
                )
    Map.mode = _(\
                panel = _(method = "pmode", dtype = "series"),\
                else = _(method = "mode", dtype = "series")\
                )

    Map.first = _(\
                panel = _(method = "pfirst", dtype = "series"),\
                else = _(method = "first", dtype = "series")\
                )

    Map.last = _(\
                panel = _(method = "plast", dtype = "series"),\
                else = _(method = "last", dtype = "series")\
                )

    return Map["@method"]
end function


function scalar get_number_of_cross_units (void)
    /* Return number of cross-sectional units. In case of time-series and cross-sectional data, set N = 1.
    return: int, number of cross-sectional units. */

    return $datatype == 3 ? ($nobs / $pd) : 1
end function


function scalar get_number_of_time_periods (void)
    /* Return number of time-periods. In case of cross-sectional data, T=1.
    return: int, number of time-periods. */

    if $datatype == 1
        return 1
    elif $datatype == 2
        return $nobs
    elif $datatype == 3
        return $pd
    else
        funcerr "Data type not supported."
    endif
end function


function series mode (const series y)
    /* Compute most frequent value for each cross-sectional unit.
    return: series, constant value for each cross-sectional unit.

    Dependency: extra.gfn */

    errorif(!nobs(y), "No valid observations.")

    set skip_missing off

    scalar N = get_number_of_cross_units()
    scalar T = get_number_of_time_periods()
    matrix m = mshape({y}, T, N)
    matrix mat = mshape(NA, N, 1)

    loop i=1..N
        mat[i] = onemode(m[,i])[1]
    endloop

    set skip_missing on

    series ret = $datatype == 3 ? pexpand(mat) : mat
    string desc = get_description("mode", argname(y), -1)
    setinfo ret --description="@desc"

    return ret
end function


function series helper_first_last (const series y, const bool do_last)
    /* Wrapper for first() and last()
    return: series, respective values. */

    set skip_missing off

    scalar N = get_number_of_cross_units()
    scalar T = get_number_of_time_periods()
    matrix m = mshape({y}, T, N)
    matrix ret = mshape(NA, N, 1)

    loop i=1..N
        matrix mask = ok(m[,i])
        matrix vals = selifr(m[,i], mask)

        scalar idx = do_last == TRUE ? rows(vals) : 1
        ret[i] = sum(mask) ? vals[idx] : NA
    endloop

    set skip_missing on

    return $datatype == 3 ? pexpand(ret) : ret
end function


function series first (const series y)
    /* Compute first valid value for each panel unit. Expand this constant value for each unit to a series. */

    series ret = helper_first_last(y, FALSE)

    string desc = get_description("first", argname(y), -1)
    setinfo ret --description="@desc"

    return ret
end function


function series last (const series y)
    /* Compute last valid value for each panel unit. Expand this constant value for each unit to a series. */

    series ret = helper_first_last(y, TRUE)

    string desc = get_description("last", argname(y), -1)
    setinfo ret --description="@desc"

    return ret
end function


function bundle sts_default_values (void)
    /* Default values of STS model. */

    bundle B = null

    # seasonal: 0 = none, 1 = stochastic with trigonometric terms,
    # 2 = stochastic with dummies, 3 = deterministic dummies
    B.seasonal = 2
    # slope 0 = none, 1 = stochastic, 2 = deterministic
    B.slope = 1
    # type of trend: 1 = stochastic, 2 = deterministic
    B.trend = 1
    B.irregular = TRUE

    return B
end function


function series impute_by_sts (const series y "Target series",
                               bundle self[null] "Optional parameters")
    /* Impute missing values by means of an basic structural time-series model.

    Dependency: StrucTiSM.gfn */

    errorif($datatype != 2, "Works only on time-series dataset.")
    series y_filled = NA

    bundle defaults = sts_default_values()
    if exists(self)
        self = self + defaults # over-write defaults
    else
        self = defaults
    endif

    bundle Mod = STSM_setup(y, self.irregular, self.trend, self.slope,
                            self.seasonal)

    STSM_estimate(&Mod)
    list components = STSM_components(Mod)

    errorif(nelem(components) == 0,\
            "Error: Failed to estimate model parameters.")

    series y_filled = ok(y) ? y : y_level + y_seas

    return y_filled
end function



function string get_description (const string fname,
                                 const string yname,
                                 scalar value)
    /* Retrieve series' description.
    return: string */

    scalar value = !exists(value) ? 1 : value

    bundle B
    B.ppolyfit = sprintf("polynomial fit of %s (order=%d)", yname, value)
    B.cum = sprintf("cumuluated values of %s", yname)
    B.pcum = sprintf("cumuluated values of %s", yname)
    B.pquantile = sprintf("quantile (q=%g) values of %s", value, yname)
    B.pxfraction = sprintf("cross-sectional fractions of %s (value=%g)", yname, value)
    B.first = sprintf("first valid values of %s", yname)
    B.last = sprintf("last valid values of %s", yname)
    B.mode = sprintf("mode of %s", yname)
    B.median = sprintf("median of %s", yname)
    B.pxmean = sprintf("cross-sectional means of %s", yname)

    return B["@fname"]
end function
